	assume	adl=1

	section	.text
	public	__lmuls_fast, __lmulu_fast
__lmuls_fast:
__lmulu_fast:

; Multiplies EUHL by AUBC and returns the 32-bit product euhl.
;    I: AUBC=multiplier, EUHL=multiplicand, ADL=1
;    O: a=EUHL*AUBC-L*C>>24, ubc=L*C, deu=0, d=H*C>>8, euhl=EUHL*AUBC
;   FO: sz(e), vc((EUHL*AUBC-L*C&0xFFFFFFFF)+L*C)
;   CC: (86+2*cf)*r(PC)+9*w(SPL)+9*r(SPL)+42
;				; a		ubc		ude		uhl		uix		u(sp)
Mul_EUHL_AUBC_EUHL_Fast:	;--------------+---------------+---------------+---------------+---------------+------------------------
	push	ix		;										u(sp)=UIX
	ld	d, c		;				 de=CE
	mlt	de		;				ude=E*C
	ld	d, a		;				 d =A
	ld	a, e		; a=E*C
	ld	e, l		; 				 de=AL
	ld	ixl, e		;								ixl=L
	mlt	de		;				ude=L*A
	add	a, e		; a=E*C+L*A
	dec	sp
	push	bc
	inc	sp
	inc	sp
	push	hl
	inc	sp
	inc	sp		;											 (sp)=[BCU][HLU]
	ld	d, h		;				 d =H
	ld	ixh, d		;								 ix=HL
	pop	hl		;						 hl=[BCU][HLU]				--
	ld	e, h		;				 de=H[BCU]
	mlt	de		;				ude=H*BCU
	add	a, e		; a=E*C+H*BCU+L*A
	ld	d, b		;				 d =B
	ld	e, l		;				 de=B[HLU]
	mlt	de		;				ude=HLU*B
	add	a, e		; a=E*C+HLU*B+H*BCU+L*A
	ld	d, h		;				 d =BCU
	ld	e, ixl		;				 de=[BCU]L
	ld	h, c		;						 hl=C[HLU]
	mlt	de		;				ude=L*BCU
	mlt	hl		;						uhl=HLU*C
	add	hl, de		;						uhl=HLU*C+L*BCU
	ld	d, b		;				 d =B
	ld	e, ixh		;				 de=BH
	mlt	de		;				ude=H*B
	add	hl, de		;						uhl=HLU*C+H*B+L*BCU
	add	a, h		; a=(HLU*C+H*B+L*BCU>>8)+E*C+HLU*B+H*BCU+L*A
	ld	h, l		;						 h =HLU*C+H*B+L*BCU
	ld	l, 0		;						 hl=HLU*C+H*B+L*BCU<<8
	ld	d, b		;				 d =B
	ld	e, ixl		;				 de=BL
	mlt	de		;				ude=L*B
	add.s	hl, de		;						uhl=L*B+(HLU*C+H*B+L*BCU<<8)&0xFFFF
	adc	a, 0		; a=L*B+(HLU*C+H*B+L*BCU<<8)+(E*C+HLU*B+H*BCU+L*A<<16)>>16
	ld	d, ixh		;				 d =H
	ld	e, c		;				 de=HC
	mlt	de		;				ude=H*C
	add	hl, de		;						uhl=H*C+(L*B+(HLU*C+H*B+L*BCU<<8)&0xFFFF)
	add	hl, hl
	add	hl, hl
	add	hl, hl
	add	hl, hl
	add	hl, hl
	add	hl, hl
	add	hl, hl
	add	hl, hl		;						uhl=(H*C+L*B<<8)+(HLU*C+H*B+L*BCU<<16)
	adc	a, l		; a=H*C+L*B+(HLU*C+H*B+L*BCU<<8)+(E*C+HLU*B+H*BCU+L*A<<16)>>16
	ld	b, ixl		;		 bc =LC
	mlt	bc		;		ubc=L*C
	add	hl, bc		;						uhl=L*C+(H*C+L*B<<8)+(HLU*C+H*B+L*BCU<<16)
	pop	ix		;								uix=UIX		--
	ld	e, a		;				  e=H*C+L*B+(HLU*C+H*B+L*BCU<<8)+(E*C+HLU*B+H*BCU+L*A<<16)>>16
	ret	nc		; nc => e=L*C+(H*C+L*B<<8)+(HLU*C+H*B+L*BCU<<16)+(E*C+HLU*B+H*BCU+L*A<<24)>>24
	inc	e		; 				e=L*C+(H*C+L*B<<8)+(HLU*C+H*B+L*BCU<<16)+(E*C+HLU*B+H*BCU+L*A<<24)>>24
	ret

; (EUHL*AUBC)&0xFFFFFFFF=L*C+(H*C+L*B<<8)+(HLU*C+H*B+L*BCU<<16)+(E*C+HLU*B+H*BCU+L*A<<24)+(E*B+HLU*BCU+H*A<<32)+(E*BCU+HLU*A<<40)+(E*A<<48)&0xFFFFFFFF
;			=L*C+(H*C+L*B<<8)+(HLU*C+H*B+L*BCU<<16)+(E*C+HLU*B+H*BCU+L*A<<24)&0xFFFFFFFF
