; Performs 48-bit bit reversal
;
; Returns:
; ude:uhl = ude:uhl bits reversed

	assume	adl=1

	section	.text
	public	__i48bitrev
__i48bitrev:
	push af
	
	ld a, h ; a=H
	ld h, l
	ld l, a ; uhl=[HLU]LH
	call __internal_bitrev_byte	; a=HLU', uhl=LH0
	ld l, a ; uhl=LH[HLU']
	call	__internal_bitrev_byte ; a=L', uhl=H[HLU']0
	ld l, h
	ld h, a ; uhl=HL'[HLU']
	call __internal_bitrev_byte ; a=H', uhl=L'[HLU']0
	ld l, h
	ld h, a ; uhl=L'H'[HLU']
	
	ex de, hl

	ld a, h ; a=H
	ld h, l
	ld l, a ; uhl=[HLU]LH
	call __internal_bitrev_byte ; a=HLU', uhl=LH0
	ld l, a ; uhl=LH[HLU']
	call __internal_bitrev_byte	; a=L', uhl=H[HLU']0
	ld l, h
	ld h, a ; uhl=HL'[HLU']
	call __internal_bitrev_byte ; a=H', uhl=L'[HLU']0
	ld l, h
	ld h, a ; uhl=L'H'[HLU']

	pop af
	ret
	extern __internal_bitrev_byte