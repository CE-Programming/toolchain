	assume	adl=1

	section	.text

	public	__imulu_b_fast

__imulu_b_fast:

; Multiplies UHL by A (unsigned) and returns the 24-bit product uhl.
;    I: A=multiplier, UHL=multiplicand, ADL=1
;    O: uhl=UHL*A
;   CC: 28*r(PC)+9*r(SPL)+6*w(SPL)+13
;   CC: 27 bytes | 28F + 9R + 6W + 13
Mul_UHL_A_UHL_Fast:
	dec	sp
	push	hl
	inc	sp
	pop	de	; D = UHL, E = H
	ld	e, a
	mlt	de	; DE = UHL * A
	ld	b, l
	ld	c, a
	mlt	bc	; BC = L * A
	ld	l, a
	mlt	hl	; HL = H * A
	ld	a, h
	add	a, e
	ld	h, a
	add	hl, hl
	add	hl, hl
	add	hl, hl
	add	hl, hl
	add	hl, hl
	add	hl, hl
	add	hl, hl
	add	hl, hl
	add	hl, bc
	ret
