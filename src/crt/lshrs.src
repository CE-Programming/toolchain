	.assume	adl=1

	.section	.text
	.global	__lshrs
	.type	__lshrs, @function
	.global	__lshrs.hijack_lshru
	.type	__lshrs.hijack_lshru, @function

.ifdef PREFER_OS_CRT

	.set	__lshrs, 0x0001E8

.else

__lshrs:
; Suboptimal for large shift amounts
; CC: if C!=0: C*(15*r(PC)+3*r(SPL)+3*w(SPL)+4)+13*r(PC)+9*r(SPL)+6*w(SPL)+1
;     if C==0: 4*r(PC)+3*r(SPL)+2
	inc	l
	dec	l
	ret	z
	push	hl
	push	bc
	ld	b, l
	or	a, a
	sbc	hl, hl
	add	hl, sp
.L.loop:
	sra	a
__lshrs.hijack_lshru:
	inc	hl
	inc	hl
	rr	(hl)
	dec	hl
	rr	(hl)
	dec	hl
	rr	(hl)
	djnz	.L.loop
	pop	bc
	pop	hl
	ret

.endif
