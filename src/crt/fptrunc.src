	.assume	adl=1

;-------------------------------------------------------------------------------

	.section	.text

	.global	__fpfloor
	.type	__fpfloor, @function

; IEEE single precision floor
; aubc = floor(aubc)
__fpfloor: ; CHECK: same(bitcast(float, pair8_24_t, { out.BC, out.A }), floorf(bitcast(float, pair8_24_t, { in.BC, in.A }))) && out.DE == in.DE && out.HL == in.HL && out.IX == in.IX && out.IY == in.IY
	cp	a, 080h
	jr	c, __fptrunc

	.local	__fpfloor.roundup

__fpfloor.roundup:
	push	bc
	ex	(sp), hl
	adc	hl, hl
	ld	h, a
	rla
	jr	z, .mantissa.zero
	sub	a, 07Fh
	jr	c, .return.one
	sub	a, 24
	jr	nc, .return
	push	bc
	cpl
	ld	c, a
	ld	a, h
	sbc	hl, hl
	call	__ishl
	ex	(sp), hl
	pop	bc
	dec	hl
	call	__iand
	or	a, a
	sbc	hl, bc
	sbc	a, -1
	push	hl
	pop	bc
	ld	h, a
.return:
	ld	a, h
	pop	hl
	ret

.mantissa.zero:
	dec	a
	cp	a, 07Fh - 1
	jr	nc, .return
.return.one:
	ld	bc, 0800000h
	ld	a, h
	or	a, 07Fh >> 1
	pop	hl
	ret

;-------------------------------------------------------------------------------

	.section	.text

	.global	__fpceil
	.type	__fpceil, @function

; IEEE single precision ceiling
; aubc = ceil(aubc)
__fpceil: ; CHECK: same(bitcast(float, pair8_24_t, { out.BC, out.A }), ceilf(bitcast(float, pair8_24_t, { in.BC, in.A }))) && out.DE == in.DE && out.HL == in.HL && out.IX == in.IX && out.IY == in.IY
	rlca
	rrca
	jr	nc, __fpfloor.roundup

	; require	__fptrunc

;-------------------------------------------------------------------------------

	; .section	.text

	.global	__fptrunc
	.type	__fptrunc, @function

; IEEE single precision truncation
; aubc = trunc(aubc)
__fptrunc: ; CHECK: same(bitcast(float, pair8_24_t, { out.BC, out.A }), truncf(bitcast(float, pair8_24_t, { in.BC, in.A }))) && out.DE == in.DE && out.HL == in.HL && out.IX == in.IX && out.IY == in.IY
	push	bc
	ex	(sp), hl
	add	hl, hl
	ld	h, a
	rla
	sub	a, 07Fh
	jr	c, __fptrunc.return.zero
	sub	a, 24
	jr	nc, __fptrunc.return
	ld	l, c
	cpl
	ld	c, a
	ld	a, l
	push	hl
	sbc	hl, hl
	call	__ishl
	ld	c, a
	call	__iand
	ex	(sp), hl
	pop	bc
__fptrunc.return:
	ld	a, h
	pop	hl
	ret

__fptrunc.return.zero:
	ld	bc, 0
	ld	a, h
	and	a, 080h
	pop	hl
	ret

;-------------------------------------------------------------------------------

	.extern	__iand
	.extern	__ishl
