; Performs 48-bit bitwise xor
;
; Returns:
; ude:uhl = ude:uhl ^ uiy:ubc

	.assume	adl=1

	.section	.text
	.global	__i48xor
	.type	__i48xor, @function

__i48xor:
; CC: 41 bytes | 42F + 22R + 17W + 1
	push	af

	; Deal with Upper Byte
	push	hl
	push	bc
	push	de
	scf
	sbc	hl, hl
	add	hl, sp	; uhl = &iyu
	push	iy

	ld	de, 3
	ld	a, (hl)	; a = iyu
	add	hl, de
	xor	a, (hl)	; a ^= deu
	ld	(hl), a	; deu = a

	add	hl, de
	ld	a, (hl)	; a = bcu
	add	hl, de
	xor	a, (hl)	; a ^= hlu
	ld	(hl), a	; hlu = a

	; Load values back into registers and process low and high bytes
	pop	bc
	pop	de

	ld	a, d
	xor	a, b	; = iyh
	ld	d, a

	ld	a, e
	xor	a, c	; = iyl
	ld	e, a

	pop	bc
	pop	hl

	ld	a, h
	xor	a, b
	ld	h, a

	ld	a, l
	xor	a, c
	ld	l, a

	pop	af
	ret
