	assume	adl=1

	section	.text

	public	__llshru

__llshru:
; Suboptimal for large shift amounts
	push	af
	push	iy
	ld	iy, 0
	add	iy, sp
	ld	a, (iy + 9)
	or	a, a
	jr	z, __llshr_common.finish
	push	de
	push	hl
	srl	b
	jr	__llshr_common.hijack_llshru

	section	.text

	public	__llshrs

__llshrs:
; Suboptimal for large shift amounts
	push	af
	push	iy
	ld	iy, 0
	add	iy, sp
	ld	a, (iy + 9)
	or	a, a
	jr	z, __llshr_common.finish
	push	de
	push	hl

	section	.text

	private	__llshr_common
__llshr_common:
.loop:
	sra	b
	private	__llshr_common.hijack_llshru
.hijack_llshru:
	rr	c
	rr	(iy - 1)
	rr	d
	rr	e
	rr	(iy - 4)
	rr	h
	rr	l
	dec	a
	jr	nz, .loop

	ld	(iy - 3), e
	ld	(iy - 2), d
	ex	de, hl
	pop	hl
	ld	l, e
	ld	h, d
	pop	de
	private	__llshr_common.finish
.finish:
	pop	iy
	pop	af
	ret
