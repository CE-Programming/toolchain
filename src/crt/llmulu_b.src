	assume	adl=1

	section	.text

	public	__llmulu_b

__llmulu_b:
; Multiplies BC:UDE:UHL by (SP) and returns the 64-bit unsigned product bc:ude:uhl.
;    I: (SP) = 8-bit multiplier, BC:UDE:UHL = multiplicand, ADL=1
;    O: bc:ude:uhl = BC:UDE:UHL * (SP)
;   CC: 101*r(PC)+21*r(SPL)+18*w(SPL)+33
;   CC: 100 bytes | 101F + 21R + 18W + 33
	push	iy
	ld	iy, 0
	add	iy, sp
	push	de
	push	hl
	push	bc
	ld	b, (iy + 6)
	ld	c, 0
	ld	h, b
	mlt	hl
	ld	(iy - 6), l
	ld	d, b
	ld	e, (iy - 5)
	mlt	de
	ld	l, h
	ld	h, c
	add	hl, de
	ld	(iy - 5), l
	ld	d, b
	ld	e, (iy - 4)
	mlt	de
	ld	l, h
	ld	h, c
	add	hl, de
	ld	(iy - 4), l
	ld	d, b
	ld	e, (iy - 3)
	mlt	de
	ld	l, h
	ld	h, c
	add	hl, de
	ld	(iy - 3), l
	ld	d, b
	ld	e, (iy - 2)
	mlt	de
	ld	l, h
	ld	h, c
	add	hl, de
	ld	(iy - 2), l
	ld	d, b
	ld	e, (iy - 1)
	mlt	de
	ld	l, h
	ld	h, c
	add	hl, de
	ld	(iy - 1), l

	pop	de
	ld	l, h
	ld	c, d
	ld	d, b
	mlt	bc
	ld	h, c
	mlt	de

	add	hl, de
	ld	b, h
	ld	c, l

	pop	hl
	pop	de
	pop	iy
	ret
