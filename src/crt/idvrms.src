	.assume	adl=1

	.section	.text
	.global	__idvrms
	.type	__idvrms, @function

__idvrms:
; I: UHL=dividend, UBC=divisor
; O: ude=UHL%UBC, uhl=UHL/UBC

	push	bc
	push	af

	ex	de, hl

	sbc	hl, hl
	ccf
	sbc	hl, bc
	inc	hl
	jp	m, .L.neg_divisor_skip
	cpl
	push	hl
	pop	bc
.L.neg_divisor_skip:

	sbc	hl, hl
	ccf
	sbc	hl, de
	inc	hl
	jp	p, .L.pos_dividend_skip
	cpl
	ex	de, hl
.L.pos_dividend_skip:

	call	__idvrmu

	call	p, __ineg

	ld	b, a
	pop	af
	cp	a, b
	pop	bc
	ex	de, hl
	ret	nz
	jp	__ineg

	.extern	__idvrmu
	.extern	__ineg
