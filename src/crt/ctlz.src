	.assume	adl=1

	.section	.text.__bctlz
	.global	__bctlz
	.type	__bctlz, @function

__bctlz:
	cp	a, 010h
	jr	c, .L.low4
	cp	a, 040h
	jr	c, .L.low6
	add	a, a
	sbc	a, a
	inc	a
	ret
.L.low6:
	add	a, -020h
	sbc	a, a
	add	a, 3
	ret
.L.low4:
	cp	a, 4
	jr	c, .L.low2
	add	a, -8
	sbc	a, a
	add	a, 5
	ret
.L.low2:
	add	a, -3
	cpl
	adc	a, 6
	ret

	.section	.text.__sctlz
	.global	__sctlz
	.type	__sctlz, @function

__sctlz:
	ld	a, h
	or	a, a
	jr	nz, __bctlz
	ld	a, l
	.local	__sctlz.hijack
__sctlz.hijack:
	call	__bctlz
	add	a, 8
	ret

	.section	.text.__ictlz
	.global	__ictlz
	.type	__ictlz, @function

__ictlz:
	dec	sp
	push	hl
	inc	sp
	pop	af
	or	a, a
	jr	nz, __bctlz
	or	a, h
	jr	nz, __sctlz.hijack
	ld	a, l
	call	__bctlz
	add	a, 16
	ret

	.section	.text.__lctlz
	.global	__lctlz
	.type	__lctlz, @function

__lctlz:
	ld	a, e
	or	a, a
	jr	nz, __bctlz
	call	__ictlz
	add	a, 8
	ret

	.section	.text.__llctlz
	.global	__llctlz
	.type	__llctlz, @function

__llctlz:
	ld	a, b
	or	a, a
	jr	nz, __bctlz
	or	a, c
	jr	nz, __sctlz.hijack
	call	__i48ctlz
	add	a, 16
	ret

	.section	.text.__i48ctlz
	.global	__i48ctlz
	.type	__i48ctlz, @function

__i48ctlz:
	ex	de, hl
	add	hl, de
	or	a, a
	sbc	hl, de
	jr	z, .L.low
	call	__ictlz
	ex	de, hl
	ret
.L.low:
	ex	de, hl
	call	__ictlz
	add	a, 24
	ret
