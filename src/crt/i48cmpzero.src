; Performs 48-bit comparison with 0
;
; Returns:
; z status flag set if ude:uhl == 0, s status flag set if ude:uhl is negative

	.assume	adl=1

	.section	.text
	.global	__i48cmpzero
	.type	__i48cmpzero, @function

__i48cmpzero:
; CC: 13 bytes
; Minimum:  8F + 3R + 2
; Maximum: 14F + 3R + 3
	; check msb's == 0
	ex	de, hl
	add	hl, de
	or	a, a
	sbc	hl, de	; z set if de = 0, s set if de is negative (z also reset)
	ex	de, hl
	ret	nz
	; check lsb's == 0
	; in this path, de = 0 and carry is reset
	sbc	hl, de
	ret	p
	; special case if bit 23 is set
	inc	e	; reset z and s flags
	dec	de
	ret
