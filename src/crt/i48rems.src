    assume adl=1

    section .text
    public __i48rems
; Result in ude:uhl
__i48rems:
    push iy
    push bc
    
    ; iy sign check
    push hl
    lea hl, iy
    add hl, de
    or a, a
    sbc hl, de
    pop hl
    call m, __uiyubcNeg

    ; de sign check
    ex de, hl
    add hl, de
    or a, a
    sbc hl, de
    ex de, hl
    push af
    call m, __i48neg
    pop af

    call __i48dvrmu
    jq m, .negExit
    jr .exit

; uhl:ude (numerator) negative case
.negExit:
    lea de, iy+0
    ld hl, 0
    add hl, bc
    call __i48neg
    jr .cleanup
; uhl:ude (numerator) positive case
.exit:
    lea de, iy+0
    ld hl, 0
    add hl, bc
.cleanup:
    pop bc
    pop iy
    ret

    extern	__i48dvrmu
    extern  __i48neg
    extern  __uiyubcNeg