    assume adl=1

    section .text
    public __i48rems
; Result in ude:uhl
__i48rems:
    push iy
    push bc
    
    ; iy sign check
    push hl
    lea hl, iy
    add hl, bc
    or a, a
    sbc hl, bc
    pop hl
    jq m, .oneArgNegUiyUbc

    ; de sign check
    ex de, hl
    add hl, de
    or a, a
    sbc hl, de
    ex de, hl
    jq m, .oneArgNegUdeUhl
    call __i48dvrmu
    jq .exit

.oneArgNegUiyUbc:
    ; uiyubc = -uiyubc
    call __uiyubcNeg

    ; de sign check
    ex de, hl
    add hl, de
    or a, a
    sbc hl, de
    ex de, hl
    jq m, .twoArgNeg
    call __i48dvrmu
    jq .exit

.oneArgNegUdeUhl:
    call __i48neg
    call __i48dvrmu
    jq .negExit

.twoArgNeg:
    call __i48neg
    call __i48dvrmu
; uhl:ude (numerator) negative case
.negExit:
    lea de, iy+0
    push bc
    pop hl
    call __i48neg
    
    pop bc
    pop iy
    ret
; uhl:ude (numerator) positive case
.exit:
    lea de, iy+0
    push bc
    pop hl

    pop bc
    pop iy
    ret

    extern	__i48dvrmu
    extern  __i48neg
    extern  __uiyubcNeg