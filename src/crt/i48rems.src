; Performs 48-bit signed remainder
;
; Returns:
; ude:uhl = ude:uhl % uiy:ubc

	.assume	adl=1

	.section	.text
	.global	__i48rems
	.type	__i48rems, @function

__i48rems:
	push	iy
	push	bc

	; divisor sign check
	push	hl
	lea	hl, iy + 0
	add	hl, hl
	call	c, __uiyubcNeg

	; dividend sign check
	sbc	hl, hl
	adc	hl, de
	pop	hl
	push	af
	call	m, __i48neg
	pop	af

	call	__i48dvrmu
	lea	de, iy + 0
	push	bc
	pop	hl

	pop	bc
	pop	iy

	ret	p
	; negate remainder if dividend was negative
	jp	__i48neg

	.extern	__i48dvrmu
	.extern	__i48neg
	.extern	__uiyubcNeg
