; Performs 48-bit unsigned comparison
;
; Arguments:
; ude:uhl = left-hand side
; uiy:ubc = right-hand side
;
; Returns:
; Z and C flags updated according to comparison

	assume	adl=1

	section	.text
	public	__i48cmpu
__i48cmpu:
; CC: 15 bytes
; Minimum: 11F + 6R + 3W + 2
; Maximum: 16F + 6R + 3W + 2
	push	hl
	or	a, a
	lea	hl, iy
	sbc	hl, de	; subtract IY - DE, which is the other way around
	ccf		; complement carry
	pop	hl
	ret	nz
	or	a, a
	sbc	hl, bc	; subtract the low parts the normal way
	add	hl, bc	; causes the same carry, preserves Z
	; don't complement carry
	ret
