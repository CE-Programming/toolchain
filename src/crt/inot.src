	assume	adl=1

	section	.text
	public	__inot

if PREFER_OS_CRT

__inot := 000164h

else

__inot:
; UHL = ~UHL
;   CC: 11*r(PC)+3*r(SPL)+1
;   CC: 10 bytes | 11F + 3R + 1
	add	hl, de	; uhl=UHL+UDE
	ex	de, hl	; ude=UHL+UDE, uhl=UDE
	scf
	sbc	hl, de	; uhl=UDE-(UHL+UDE)-1
			;    =UDE-UHL-UDE-1
			;    =-UHL-1
			;    =~UHL
	ex	de, hl	; ude=~UHL, uhl=UHL+UDE
	add	hl, de	; uhl=UHL+UDE+~UHL
			;    =UHL+UDE+(-UHL-1)
			;    =UHL+UDE-UHL-1
			;    =UDE-1
	inc	hl	; uhl=UDE
	ex	de, hl	; ude=UDE, uhl=~UHL
	ret

end if
