; Performs 48-bit signed division
;
; Returns:
; ude:uhl = ude:uhl / uiy:ubc

	.assume	adl=1

	.section	.text
	.global	__i48divs
	.type	__i48divs, @function

__i48divs:
	push	af
	push	iy
	push	bc

	; divisor sign check
	push	hl
	lea	hl, iy + 0
	add	hl, hl
	sbc	a, a
	call	c, __uiyubcNeg

	; dividend sign check
	ld	hl, $800000
	add	hl, de
	pop	hl
	rla
	rrca
	call	c, __i48neg

	call	__i48dvrmu

	; negate quotient if exactly one of the dividend and divisor is negative
	add	a, a
	call	pe, __i48neg

	pop	bc
	pop	iy
	pop	af
	ret

	.extern	__i48dvrmu
	.extern	__i48neg
	.extern	__uiyubcNeg
