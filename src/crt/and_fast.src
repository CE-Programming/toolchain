	assume	adl=1

	section	.text
	public	__land_fast
__land_fast:
; CC: 24*r(PC)+8*r(SPL)+7*w(SPL)+1
	and	a, e
	ld	e, a
	require	__iand_fast

	section	.text
	public	__iand_fast
__iand_fast:
; CC: 22*r(PC)+8*r(SPL)+7*w(SPL)+1
	push	hl
	scf
	sbc	hl, hl
	add	hl, sp
	push	bc
	ld	a, (hl)
	inc	hl
	ld	sp, hl
	inc	hl
	inc	hl
	and	a, (hl)
	ld	(hl), a
	pop	hl
	require	__sand_fast

	section	.text
	public	__sand_fast
__sand_fast:
; CC: 8*r(PC)+3*r(SPL)+1
	ld	a, h
	and	a, b
	ld	h, a
	ld	a, l
	and	a, c
	ld	l, a
	ret
