	.assume	adl=1

	.section	.text
	.global	__idvrmu
	.type	__idvrmu, @function

.ifdef PREFER_OS_CRT

	.set	__idvrmu, 0x000144

.else

__idvrmu:
; I: UHL=dividend, UBC=divisor
; O: ude=UHL/UBC, uhl=UHL%UBC

	ex	de, hl

	push	af
	ld	a, 24
	.global	__idvrmu.hijack_a_iters_ude_dividend
	.type	__idvrmu.hijack_a_iters_ude_dividend, @function
__idvrmu.hijack_a_iters_ude_dividend:

	or	a, a
	sbc	hl, hl

.L.loop:
	ex	de, hl
	add	hl, hl
	ex	de, hl
	adc	hl, hl

	sbc	hl, bc
	inc	e

	jr	nc, .L.restore_skip
	add	hl, bc
	dec	e
.L.restore_skip:

	dec	a
	jr	nz, .L.loop

	pop	af
	ret

.endif
