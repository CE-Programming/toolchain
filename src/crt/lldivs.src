	.assume	adl=1

	.section	.text
	.global	__llrems
	.type	__llrems, @function
	.global	__lldivs
	.type	__lldivs, @function

__llrems:

	scf
	jr	.L.hijack

__lldivs:

	or	a, a
.L.hijack:

	push	iy
	push	af

	ld	iy, 2
	add	iy, sp

	ld	a, b
	ld	(iy), a
	xor	a, (iy + 14)
	ld	(iy + 15), a

	xor	a, b
	rla
	jr	nc, .L.neg_divisor_skip

	push	bc
	push	hl

	lea	hl, iy + 7
	xor	a, a
	ld	c, a
	ld	b, 8

.L.neg_divisor_loop:
	ld	a, c
	sbc	a, (hl)
	ld	(hl), a
	inc	hl
	djnz	.L.neg_divisor_loop

	pop	hl
	pop	bc
.L.neg_divisor_skip:

	bit	7, b
	call	nz, __llneg

	or	a, a
	call	__lldvrmu.hijack

	pop	af
	bit	7, (iy + 8)
	jr	c, .L.div_quotient_skip
	ld	hl, (iy + 15)
	ld	de, (iy + 18)
	ld	bc, (iy + 21)
	bit	7, (iy + 23)
.L.div_quotient_skip:

	pop	iy

	ret	z
	jp	__llneg

	.extern	__lldvrmu.hijack
	.extern	__llneg
