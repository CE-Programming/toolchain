	.assume	adl=1

	.section	.text

	.global	__fpmul
	.type	__fpmul, @function

; IEEE single precision multiplication
; aubc = aubc * euhl
__fpmul: ; CHECK: same(bitcast(float, pair8_24_t, { out.BC, out.A }), bitcast(float, pair8_24_t, { in.BC, in.A }) * bitcast(float, pair8_24_t, { in.HL, in.E })) && out.DE == in.DE && out.HL == in.HL && out.IX == in.IX && out.IY == in.IY
	push	de
	push	hl
	xor	a, e
	push	af
	xor	a, e
	push	bc
	call	__fppop1
	ex	(sp), hl
	ld	d, e
	ld	e, a
	call	__fppop2
	inc	e
	jr	z, .nonfinite.1
	inc	d
	jr	z, .nonfinite.2
	ld	a, d
	ld	d, b; 0
	rlc	e
	ccf
	rr	e
	jr	nc, .subtract
	add	a, e
	jr	nc, .continue
.overflow:
	pop	af
	pop	af
	pop	hl
	pop	de
	or	a, 07Fh
	ret
.subtract:
	add	a, e
	jr	z, .subnormal
	jr	c, .continue
	cp	a, -23
	jr	c, .underflow
.subnormal:
	dec	a
	ld	d, a
.continue:
	push	hl
	push	ix
	ld	ix, -7
	add	ix, sp
	ld	sp, ix
	ld	c, l
	ld	h, (ix + 13)
	mlt	hl
	ld	(ix + 0), l
	ld	l, h
	ld	h, 0
	ld	b, (ix + 14)
	mlt	bc
	add	hl, bc
	ld	c, (ix + 11)
	ld	b, (ix + 13)
	mlt	bc
	add.s	hl, bc
	ld	(ix + 1), l
	ld	l, h
	ld	h, 0
	rl	h
	ld	c, (ix + 10)
	ld	b, (ix + 15)
	mlt	bc
	add	hl, bc
	ld	c, (ix + 11)
	ld	b, (ix + 14)
	mlt	bc
	add	hl, bc
	ld	bc, (ix + 12)
	mlt	bc
	add	hl, bc
	ld	(ix + 2), hl
	ld	hl, (ix + 3)
	inc	hl
	dec.s	hl
	ld	c, (ix + 11)
	ld	b, (ix + 15)
	mlt	bc
	add	hl, bc
	ld	c, (ix + 12)
	ld	b, (ix + 14)
	mlt	bc
	add	hl, bc
	ld	(ix + 3), hl
	ld	hl, (ix + 4)
	inc	hl
	dec.s	hl
	ld	c, (ix + 12)
	ld	b, (ix + 15)
	mlt	bc
	add	hl, bc
	cp	a, d
	jr	nz, .normalized
	ld	bc, (ix + 2)
	pop	de
.normalize:
	srl	h
	rr	l
	rr	b
	rr	c
	rr	d
	rr	e
	jr	nc, .flushed
	ld	e, a
.flushed:
	inc	a
	jr	nz, .normalize
	push	de
	ld	(ix + 2), bc
.normalized:
	ld	(ix + 4), hl
	ld	c, (ix + 17)
	pop	ix
	pop	hl
	inc	sp
	ld	b, a
	ld	de, 0800000h
	add	a, -1
	call	nc, __fppack2.normalized
	call	c, __fppack2
	pop	ix
	pop	bc
	pop	bc
	pop	bc
	ex	(sp), hl
	pop	bc
	pop	de
	ret
.underflow:
	pop	af
	pop	af
	pop	hl
	pop	de
	and	a, 080h
	ld	bc, 0
	ret
.nonfinite:
	sbc	hl, bc
	jr	z, .return.2
	add	hl, bc
	pop	bc
	jr	.return.1
.nonfinite.1:
	inc	d
	jr	z, .nonfinite
	ex	de, hl
	pop	hl
	add	hl, bc
	or	a, a
	sbc	hl, bc
	ex	de, hl
	jr	nz, .return.1
	ld	h, a
.return.1:
	pop	af
	ex	(sp), hl
	pop	bc
	jr	.return
.nonfinite.2:
	add	hl, bc
	or	a, a
	sbc	hl, bc
	jr	z, .return.nan
.return.2:
	pop	bc
	pop	af
	pop	bc
	push	bc
.return.pop:
	pop	hl
.return:
	pop	de
	or	a, 07Fh
	ret
.return.nan:
	pop	bc
	set	7, b
	pop	af
	jr	.return.pop

	.extern	__fppop1
	.extern	__fppop2
	.extern	__fppack2.normalized
	.extern	__fppack2
