	assume	adl=1

kUp           := 3
kLeft         := 2
kDown         := 4
kEnter        := 5
kClear        := 9
kRight        := 1
kQuit         := $40
kIns          := $0B
curLock       := 4
flags         := $D00080
keyExtend     := $D0058E
curRow        := $D00595		; also includes curCol
curUnder      := $D00599
_PutS         := $207C0
_PutC         := $207B8
_PutPS        := $207C8
_GetKey       := $20D8C
_CursorOn     := $208B0
_CursorOff    := $208A8
_PullDownChk  := $208BC
_ConvKeyToTok := $20E40
_GetTokString := $20874
_PutTokString := $20D74
_ClrTxtShd    := $20818

	section	.text
	public	_os_GetTokenInput
_os_GetTokenInput:
	push	ix
	ld	iy,flags
	ld	a,(iy + $0d)
	ld	(.smc.text_flag_0),a
	ld	a,(iy + $4c)
	ld	(.smc.text_flag_1),a
	set	1,(iy + $0d)		; use text buffer
	res	5,(iy + $4c)		; use text buffer
	call	_ClrTxtShd
	ld	ix,0
	add	ix,sp
	ld	hl,(ix+6)		; hl -> input string prompt
	add	hl,de
	xor	a,a
	sbc	hl,de
	ld	iy,flags
	call	nz,_PutS
	ld	hl,(curRow)
.start:
	push	hl			; save initial row/column
	or	a,a
	sbc	hl,hl
	push	hl			; save string length
	ld	hl,(ix+12)		; buffer size
	push	hl			; save remaining size
	ld	hl,(ix+9)
	push	hl			; save buf pointer
.loop:
	ld	iy,flags
	push	ix
	call	_CursorOn
.getkey:
	call	_GetKey
	call	_CursorOff
	call	_PullDownChk
	push	af
	call	_CursorOn
	pop	af
	jr	c,.getkey
	pop	ix
	cp	a,kQuit
	jr	z,.done_trampoline
	dec	a			; kRight=1
	cp	a,kEnter-kRight		; kEnter=kDown+1
	jr	c,.loop
	jr	z,.done_trampoline
	inc	a
	cp	a,kIns
	jr	z,.loop
	cp	a,kClear
	jr	z,.clear
	cp	a,$F3			; for lists? idk something is wrong with _PullDownChk
	jr	c,.conv_key
	cp	a,$FB+1
	jr	nc,.conv_key
	sub	a,$7C
	ld	(keyExtend),a
	ld	a,$FE
.conv_key:
	push	ix
	call	_ConvKeyToTok
	pop	ix
	pop	hl			; restore buf pointer
	ex	(sp),hl
	ld	bc,1
	xor	a,a
	cp	a,d			; check if 2-byte token
	sbc	hl,bc			; subtract 1 or 2 from remaining size
	ex	(sp),hl
	jr	c,.full
	or	a,d			; check if 2-byte token
	jr	z,.one_byte_token
	ld	(hl),d
	inc	hl
.one_byte_token:
	ld	(hl),e
	inc	hl
	push	hl			; save buf pointer
	push	ix
	call	_GetTokString
	pop	ix
	ld	b,(hl)			; token string length
	inc	hl
.draw_string:
	ld	de,(ix-6)
	inc	de			; increment string length
	ld	(ix-6),de
	ld	a,(hl)
	inc	hl
	call	_PutC
	djnz	.draw_string
	jr	.loop
.full:
	push	hl			; save buf pointer
.full_loop:
	push	ix
	call	_GetKey
	pop	ix
	cp	a,kEnter
.done_trampoline:
	jr	z,.done
	cp	a,kClear
	jr	nz,.full_loop
.clear:
	pop	hl			; buf pointer
	pop	hl			; remaining size
	pop	hl			; string length
	inc	hl
	pop	de			; initial row/column
	ld	(curRow),de
.clear_loop:
	ld	a,' '
	call	_PutC
	add	hl,de
	scf
	sbc	hl,de			; decrement and compare to zero
	jr	nz,.clear_loop
	ex	de,hl
	ld	(curRow),hl
	jp	.start
.done:
	call	_ClrTxtShd
	call	_CursorOff
.smc.text_flag_0 := $+1
	ld	a,0
	ld	(iy + $0d),a
.smc.text_flag_1 := $+1
	ld	a,0
	ld	(iy + $4c),a
	pop	hl			; restore buf pointer
	ld	de,(ix+9)
	or	a,a
	sbc	hl,de
	ld	sp,ix
	pop	ix
	ret
