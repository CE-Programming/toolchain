	assume	adl=1

	section	.text
	public	_os_EvalVar
_os_EvalVar:
	pop	de
	ex	(sp), hl
	push	de
	ld	iy, $D00080	; flags
	ld	de, $D005F9	; op1m
	call	$20304		; mov8b
	ld	hl, ($D02596)	; tmpcnt
	push	hl
	ld	hl, .error
	call	$20798		; push error handler
	call	$20510		; findsym
	ld	a, 141		; undefined
	jr	c, .error
	call	$20F00		; parseinp
	call	$20F30		; stoans
	call	$2079C		; pop error handler
	xor	a, a
.error:
	pop	de
	push	af
	call	$20E80		; fixtempcnt
	pop	af
	or	a, a
	sbc	hl, hl
	ld	l, a
	res	0, (iy + 7)
	ret

	section	.text
	public	_os_Eval
_os_Eval:
	ld	iy, 0
	add	iy, sp
	ld	hl, ($D02596)	; tmpcnt
	push	hl
	ld	hl, .error
	call	$20798		; push error handler
	ld	hl, (iy + 3)
	push	hl
	ld	hl, (iy + 6)
	push	hl
	inc	hl
	inc	hl
	ld	iy, $D00080	; flags
	call	$2055C		; create temp equ
	inc	de
	inc	de
	xor	a, a
	ld	(de), a
	inc	de
	ld	(de), a
	inc	de
	pop	bc
	pop	hl
	ldir
	call	$202C8		; op4 to op1
	call	$20F00		; parseinp
	call	$20F30		; stoans
	call	$2079C		; pop error handler
	xor	a, a
.error:
	pop	de
	push	af
	call	$20E80		; fixtempcnt
	pop	af
	or	a, a
	sbc	hl, hl
	ld	l, a
	res	0, (iy + 7)
	ret
