	.assume	adl=1

	.section	.text
	.global	_zx0_Decompress
	.type	_zx0_Decompress, @function

_zx0_Decompress:
	pop	bc
	pop	de
	ex	(sp), hl
	push	de
	push	bc

; -----------------------------------------------------------------------------
; ZX0 decoder by Einar Saukas & introspec
; "Turbo" version
; -----------------------------------------------------------------------------

dzx0_turbo:
	ld	iy, -1		; preserve default offset 1
	lea	bc, iy + 2	; ld bc, 1
	scf
	jr	dzx0t_start

dzx0t_new_offset:
	dec	bc
	dec	bc		; prepare negative offset
	add	a, a
	jr	nz, dzx0t_new_offset_skip
	ld	a, (hl)		; load another group of 8 bits
	inc	hl
	rla
dzx0t_new_offset_skip:
	call	nc, dzx0t_elias	; obtain offset MSB
	inc	c
	ret	z		; check end marker
	ld	b, (hl)		; obtain offset LSB
	inc	hl
	rr	c		; last offset bit becomes first length bit
	rr	b
	ld	iyl, b		; preserve new offset
	ld	iyh, c
	ld	bc, 1		; obtain length
	call	nc, dzx0t_elias
	inc	bc
dzx0t_copy:
	push	hl		; preserve source
; dzx0t_last_offset:
	lea	hl, iy + 0		; restore offset
	add	hl, de		; calculate destination - offset
	ldir			; copy from offset
	pop	hl		; restore source
	add	a, a		; copy from literals or new offset?
	jr	c, dzx0t_new_offset
dzx0t_literals:
	inc	c		; obtain length
	add	a, a
	jr	nz, dzx0t_literals_skip
dzx0t_start:
	ld	a, (hl)		; load another group of 8 bits
	inc	hl
	rla
dzx0t_literals_skip:
	call	nc, dzx0t_elias
	ldir			; copy literals
	add	a, a		; copy from last offset or new offset?
	jr	c, dzx0t_new_offset
	inc	c		; obtain length
	add	a, a
	jr	nz, dzx0t_last_offset_skip
	ld	a, (hl)		; load another group of 8 bits
	inc	hl
	rla
dzx0t_last_offset_skip:
	call	nc, dzx0t_elias
	jr	dzx0t_copy

dzx0t_elias:
	add	a, a		; interlaced Elias gamma coding
	rl	c
	add	a, a
	jr	nc, dzx0t_elias
	ret	nz
	ld	a, (hl)		; load another group of 8 bits
	inc	hl
	rla
	ret	c
	add	a, a
	rl	c
	add	a, a
	ret	c
	add	a, a
	rl	c
	add	a, a
	ret	c
	add	a, a
	rl	c
	add	a, a
	ret	c
dzx0t_elias_loop:
	add	a, a
	rl	c
	rl	b
	add	a, a
	jr	nc, dzx0t_elias_loop
	ret	nz
	ld	a, (hl)		; load another group of 8 bits
	inc	hl
	rla
	jr	nc, dzx0t_elias_loop
	ret
