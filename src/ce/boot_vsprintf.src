	.assume	adl=1

	.section	.text._boot_vsprintf
	.global	_boot_vsprintf
	.type	_boot_vsprintf, @function

	.weak	_vsprintf
	.type	_vsprintf, @function

; wraps boot_sprintf
_boot_vsprintf:
	call	__frameset0
	; Get length of format string, plus 1
	ld	hl, (ix + 9)
	push	hl
	push	hl
	call	_strlen
	pop	bc
	ex	(sp), hl
	pop	bc
	inc	bc
	; Track pointer to stack allocation
	lea	de, ix + 0
.L.count_specifiers_loop:
	ld	a, '%'
.L.count_specifiers_inner_loop:
	; Find format specifier
	cpir
	jr	nz, .L.count_done
	; Check for escaped specifier
	cpi
	jr	z, .L.count_specifiers_inner_loop
	dec	hl
	inc	bc
	ld	a, '*'
.L.count_specifiers_increment:
	; Add one parameter
	dec	de
	dec	de
	dec	de
.L.count_specifiers_format:
	; Check for end of specifier
	bit	6, (hl)
	jr	nz, .L.count_specifiers_loop
	; Check for additional variable-length parameter
	cpi
	jr	z, .L.count_specifiers_increment
	; Check for end of string
	jp	pe, .L.count_specifiers_format
.L.count_done:
	; Get length of parameters
	lea	hl, ix + 0
	or	a, a
	sbc	hl, de
	jr	z, .L.no_copy
	; Allocate space for parameters
	ex	de, hl
	ld	sp, hl
	ex	de, hl
	; Copy parameters
	push	hl
	pop	bc
	ld	hl, (ix + 12)
	ldir
.L.no_copy:
	ld	hl, (ix + 9)
	push	hl
	ld	hl, (ix + 6)
	push	hl
	call	_boot_sprintf
	ld	sp, ix
	pop	ix
	ret

	.extern	_boot_sprintf
	.extern	__frameset0
	.extern	_strlen
