	.assume	adl=1

	.section	.text._dbg_Debugger
	.global	_dbg_Debugger
	.type	_dbg_Debugger, @function

_dbg_Debugger:
	scf
	sbc	hl, hl
	ld	(hl), 2
	ret

	.section	.text._dbg_WatchpointSet
	.global	_dbg_WatchpointSet
	.type	_dbg_WatchpointSet, @function

_dbg_WatchpointSet:
	; currently have to do stupid stuff until CEmu is updated to be less dumb
	ld	iy, 0
	add	iy, sp
	scf
	sbc	hl, hl
	ld	de, (iy + 3)	; addr
	ld	bc, (iy + 6)	; size (must be 1 right now)
	ld	a, (iy + 9)	; mask
	ld	(hl), 8
	ld	(hl), 4
	or	a, a		; 0 = remove
	ret	z
	bit	0, a		; read
	jr	z, .L.noread
	bit	1, a		; write
	jr	z, .L.noreadwrite
	ld	(hl), 7
	jr	.L.checkexecute
.L.noreadwrite:
	ld	(hl), 5
	jr	.L.checkexecute
.L.noread:
	bit	1, a		; write
	jr	z, .L.checkexecute
	ld	(hl), 6
.L.checkexecute:
	bit	2, a		; execute
	ret	z
	ld	(hl), 3
	ret

	.section	.text._dbg_WatchpointRemoveAll
	.global	_dbg_WatchpointRemoveAll
	.type	_dbg_WatchpointRemoveAll, @function

_dbg_WatchpointRemoveAll:
	scf
	sbc	hl, hl
	ld	(hl), 9
	ld	(hl), 10
	ret
