	.assume	adl=1

	.section	.text

	.global	_strncmp
	.type	_strncmp, @function

.ifdef PREFER_CE_LIBC

	.set	_strncmp, 0x0000DC

.else

_strncmp:
	ld	iy, 0
	add	iy, sp
	ld	bc, (iy + 9)
	sbc	hl, hl
	adc	hl, bc
	ld	hl, (iy + 6)
	ld	de, (iy + 3)
	call	nz, .L.start	; z means BC was zero
.L.ret_zero:
	sbc	hl, hl
	ret
.L.loop:
	ret	po	; ret_zero
	or	a, a
	ret	z	; ret_zero
	inc	de
.L.start:
	ld	a, (de)
	cpi
	jr	z, .L.loop
.L.finish:
	ld	sp, iy	; ret
	dec	hl
	sub	a, (hl)
	sbc	hl, hl
	ld	l, a
	ret

.endif
