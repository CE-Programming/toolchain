	.assume	adl=1

;-------------------------------------------------------------------------------

	.section	.text.___ez80_ctzc

	.global	___ez80_ctzc
	.type	___ez80_ctzc, @function
___ez80_ctzc:
	ld	hl, 3
	add	hl, sp
	ld	a, (hl)
	jp	__bcttz

;-------------------------------------------------------------------------------

	.section	.text.___ez80_ctzi48

	.global	___ez80_ctzi48
	.type	___ez80_ctzi48, @function
___ez80_ctzi48:
	ld	hl, 6
	add	hl, sp
	ld	de, (hl)
	dec	hl
	dec	hl
	dec	hl
	ld	hl, (hl)
	jp	__i48cttz

;-------------------------------------------------------------------------------

	.section	.text.___ez80_ffsc

	.global	___ez80_ffsc
	.type	___ez80_ffsc, @function
___ez80_ffsc:
	ld	hl, 3
	add	hl, sp
	ld	a, (hl)
	or	a, a
	rla
	jp	nz, __bcttz
	ret

;-------------------------------------------------------------------------------
	
	.section	.text.___ez80_ffss

	.global	___ez80_ffss
	.type	___ez80_ffss, @function
___ez80_ffss:
	ld	hl, 3
	add	hl, sp
	ld	hl, (hl)
	ld	a, h
	or	a, l
	add	hl, hl
	jp	nz, __scttz
	ret

;-------------------------------------------------------------------------------

	.section	.text.___ez80_ffsi48

	.global	___ez80_ffsi48
	.type	___ez80_ffsi48, @function
___ez80_ffsi48:
	ld	hl, 6
	add	hl, sp
	ld	de, (hl)
	dec	hl
	dec	hl
	dec	hl
	ld	hl, (hl)
	call	__i48cttz
	inc	a
	cp	a, 49
	ret	nz
	xor	a, a
	ret

;-------------------------------------------------------------------------------

	.section	.text.___ez80_clrsbc

	.global	___ez80_clrsbc
	.type	___ez80_clrsbc, @function
___ez80_clrsbc:
	ld	hl, 3
	add	hl, sp
	ld	a, (hl)
	add	a, a
	ld	l, a
	sbc	a, a
	xor	a, l
	set	0, a
	jp	__bctlz

;-------------------------------------------------------------------------------
	
	.section	.text.___ez80_clrsbs

	.global	___ez80_clrsbs
	.type	___ez80_clrsbs, @function
___ez80_clrsbs:
	; unoptimized
	ld	hl, 3
	add	hl, sp
	ld	hl, (hl)
	add.s	hl, hl
	call	c, __snot
	set	0, l
	jp	__sctlz

;-------------------------------------------------------------------------------

	.section	.text.___ez80_clrsbi48

	.global	___ez80_clrsbi48
	.type	___ez80_clrsbi48, @function
___ez80_clrsbi48:
	ld	hl, 6
	add	hl, sp
	ld	de, (hl)
	dec	hl
	dec	hl
	dec	hl
	ld	hl, (hl)
	add	hl, hl
	ex	de, hl
	adc	hl, hl
	ex	de, hl
	call	c, __i48not
	set	0, l
	jp	__i48ctlz

;-------------------------------------------------------------------------------

	.section	.text.___ez80_parityc

	.global	___ez80_parityc
	.type	___ez80_parityc, @function
___ez80_parityc:
	xor	a, a
	ld	hl, 3
	add	hl, sp
	or	a, (hl)
	ld	a, 0
	ret	pe
	dec	a
	ret

;-------------------------------------------------------------------------------
	
	.section	.text.___ez80_paritys

	.global	___ez80_paritys
	.type	___ez80_paritys, @function
___ez80_paritys:
	ld	hl, 3
	add	hl, sp
	ld	a, (hl)
	inc	hl
	xor	a, (hl)
	ld	a, 0
	ret	pe
	dec	a
	ret

;-------------------------------------------------------------------------------

	.section	.text.___ez80_parityi48

	.global	___ez80_parityi48
	.type	___ez80_parityi48, @function
___ez80_parityi48:
	ld	hl, 3
	add	hl, sp
	ld	a, (hl)
	ld	b, 5
.L___ez80_parityi48.loop:
	inc	hl
	xor	a, (hl)
	djnz	.L___ez80_parityi48.loop
	ld	a, b
	ret	pe
	dec	a
	ret

;-------------------------------------------------------------------------------

	.section	.text.___ez80_popcountc

	.global	___ez80_popcountc
	.type	___ez80_popcountc, @function
___ez80_popcountc:
	ld	hl, 3
	add	hl, sp
	ld	a, (hl)
	jp	__bpopcnt

;-------------------------------------------------------------------------------
	
	.section	.text.___ez80_popcounts

	.global	___ez80_popcounts
	.type	___ez80_popcounts, @function
___ez80_popcounts:
	ld	hl, 3
	add	hl, sp
	ld	hl, (hl)
	jp	__spopcnt

;-------------------------------------------------------------------------------

	.section	.text.___ez80_popcounti48

	.global	___ez80_popcounti48
	.type	___ez80_popcounti48, @function
___ez80_popcounti48:
	ld	hl, 6
	add	hl, sp
	ld	de, (hl)
	dec	hl
	dec	hl
	dec	hl
	ld	hl, (hl)
	jp	__i48popcnt

;-------------------------------------------------------------------------------

	.section	.text.___ez80_bswap24

	.global	___ez80_bswap24
	.type	___ez80_bswap24, @function
___ez80_bswap24:
	ld	hl, 1
	add	hl, sp
	ld	de, (hl)
	inc	hl
	inc	hl
	inc	hl
	ld	d, (hl)
	inc	hl
	ld	e, (hl)
	ex	de, hl
	ret

;-------------------------------------------------------------------------------

	.section	.text.___ez80_bswap48

	.global	___ez80_bswap48
	.type	___ez80_bswap48, @function
___ez80_bswap48:
	pop	bc
	pop	hl
	pop	de
	push	de
	push	hl
	push	bc
	jp	__i48bswap

;-------------------------------------------------------------------------------

	.section	.text.___ez80_bitreverse48

	.global	___ez80_bitreverse48
	.type	___ez80_bitreverse48, @function
___ez80_bitreverse48:
	pop	bc
	pop	hl
	pop	de
	push	de
	push	hl
	push	bc
	jp	__i48bitrev

;-------------------------------------------------------------------------------

	.section	.text.___ez80_rotateleft24

	.global	___ez80_rotateleft24
	.type	___ez80_rotateleft24, @function
___ez80_rotateleft24:
	; unoptimized
	ld	iy, 0
	lea	de, iy + 0
	add	iy, sp
	ld	hl, (iy + 3)
	ld	a, (iy + 6)
	ld	c, 24
.L___ez80_rotateleft24.mod24:
	sub	a, c
	jr	nc, .L___ez80_rotateleft24.mod24
	add	a, c
	ret	z
	ld	b, a
.L___ez80_rotateleft24.loop:
	add	hl, hl
	adc	hl, de
	djnz	.L___ez80_rotateleft24.loop
	ret

;-------------------------------------------------------------------------------

	.section	.text.___ez80_rotateleft48

	.global	___ez80_rotateleft48
	.type	___ez80_rotateleft48, @function
___ez80_rotateleft48:
	; unoptimized
	ld	iy, 0
	lea	bc, iy + 0
	add	iy, sp
	ld	hl, (iy + 3)
	ld	de, (iy + 6)
	ld	a, (iy + 9)
	ld	c, 48
.L___ez80_rotateleft48.mod48:
	sub	a, c
	jr	nc, .L___ez80_rotateleft48.mod48
	add	a, c
	ret	z
	ld	c, 0
.L___ez80_rotateleft48.loop:
	add	hl, hl
	ex	de, hl
	adc	hl, hl
	ex	de, hl
	adc	hl, bc
	dec	a
	jr	nz, .L___ez80_rotateleft48.loop
	ret

;-------------------------------------------------------------------------------

	.section	.text.___ez80_rotateright24

	.global	___ez80_rotateright24
	.type	___ez80_rotateright24, @function
___ez80_rotateright24:
	; unoptimized
	ld	iy, 0
	lea	de, iy + 0
	add	iy, sp
	ld	hl, (iy + 3)
	ld	a, (iy + 6)
	ld	c, 24
.L___ez80_rotateright24.mod24:
	sub	a, c
	jr	nc, .L___ez80_rotateright24.mod24
	neg
	cp	a, c
	ret	z
	ld	b, a
.L___ez80_rotateright24.loop:
	add	hl, hl
	adc	hl, de
	djnz	.L___ez80_rotateright24.loop
	ret

;-------------------------------------------------------------------------------

	.section	.text.___ez80_rotateright48

	.global	___ez80_rotateright48
	.type	___ez80_rotateright48, @function
___ez80_rotateright48:
	; unoptimized
	ld	iy, 0
	lea	bc, iy + 0
	add	iy, sp
	ld	hl, (iy + 3)
	ld	de, (iy + 6)
	ld	a, (iy + 9)
	ld	c, 48
.L___ez80_rotateright48.mod48:
	sub	a, c
	jr	nc, .L___ez80_rotateright48.mod48
	neg
	cp	a, c
	ret	z
	ld	c, 0
.L___ez80_rotateright48.loop:
	add	hl, hl
	ex	de, hl
	adc	hl, hl
	ex	de, hl
	adc	hl, bc
	dec	a
	jr	nz, .L___ez80_rotateright48.loop
	ret

;-------------------------------------------------------------------------------

	.extern	__snot
	.extern	__i48not

	.extern	__bctlz
	.extern	__sctlz
	.extern	__i48ctlz

	.extern	__bcttz
	.extern	__scttz
	.extern	__i48cttz

	.extern	__bpopcnt
	.extern	__spopcnt
	.extern	__i48popcnt

	.extern	__i48bitrev

	.extern	__i48bswap
