	.assume	adl=1

	.section	.text

	.global	_atol
	.type	_atol, @function

_atol:
	pop	de
	ex	(sp), hl
	push	de
	; inlined isspace
.L.whitespace_loop:
	ld	a, (hl)
	inc	hl
	cp	a, 32
	jr	z, .L.whitespace_loop
	sub	a, 9
	add	a, -5
	jr	nc, .L.whitespace_loop

	; A = (HL - 1) - 9 + -5
	; A = (HL - 1) - 14
	xor	a, '-' - 14
	push	af
	jr	z, .L.minus_sign
	xor	a, ('+' - 14) ^ ('-' - 14)
	jr	z, .L.plus_sign
	dec	hl
.L.plus_sign:
.L.minus_sign:
	; carry is cleared
	push	hl
	pop	iy
	sbc	hl, hl
	ld	e, l
	; IY = start of the digits
	; E:UHL = 0
	jr	.L.start
.L.loop:
	; 32F + 4R + 3W + 1 per digit
	ld	d, a
	ld	a, e
	push	hl
	pop	bc
	; *= 2
	add	hl, hl
	rla
	; *= 4
	add	hl, hl
	rla
	; *= 5
	add	hl, bc
	adc	a, e
	; *= 10
	add	hl, hl
	rla
	; += digit
	ld	bc, 0
	ld	c, d
	add	hl, bc
	adc	a, b
	ld	e, a
	; next digit
	inc	iy
.L.start:
	ld	a, (iy)
	sub	a, 48
	cp	a, 10
	jr	c, .L.loop
.L.finish:
	pop	af
	; carry is cleared
	ret	nz	; A != '-' positive
	; A == '-' negative
	jp	__lneg

	.extern	__lneg
