	assume	adl=1

	section	.text

	public	__isinfl
	
; int _isinfl(long double)
__isinfl:
	; load upper 24bits
	ld	hl, 8	
	add	hl, sp
	ld	hl, (hl)
	; clear signbit
	add	hl, hl
	or	a, a
	ld	bc, $FFE000
	sbc	hl, bc
	jr	z, .mantissa_check
	; finite and common NaN
.mant_non_zero:
	or	a, a
	sbc	hl, hl
	ret
	
.mantissa_check:
	pop	bc, hl, de
	push	de, hl, bc
	; __i40cmpzero
	add	hl, de
	or	a, a
	sbc	hl, de
	jr	nz, .mant_non_zero
	; hl was zero, so the carry flag cannot have been set
	ex	de, hl
	sbc	hl, de
	jr	nz, .mant_non_zero
	; both hl and de are zero
	inc	hl
	ret
