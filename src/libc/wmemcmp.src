	assume	adl=1

	section	.text

	public	_wmemcmp

; int wmemcmp(const wchar_t *s1, const wchar_t *s2, size_t n)
_wmemcmp:
	ld	iy, 0
	add	iy, sp
	ld	bc, (iy + 9)
	sbc	hl, hl
	adc	hl, bc		; count *= 2
	ret	z		; return 0
	ld	de, (iy + 3)
	ld	hl, (iy + 6)
.loop:
	ld	a, (de)
	xor	a, (hl)
	inc	hl
	inc	de
	ld	a, (de)
	jr	nz, .low_diff
	cpi
	jr	nz, .high_diff
	inc	de
	jp	pe, .loop
	sbc	hl, hl
	ret

.high_diff:
	dec	hl
.low_diff:
	; compare high bytes
	sub	a, (hl)		; s1 - s2
	jr	nz, .skip_low_check
	; compare low bytes
	dec	de
	ld	a, (de)
	dec	hl
	sub	a, (hl)		; s1 - s2
.skip_low_check:
	sbc	hl, hl
	; A is non-zero here
	ld	l, a
	ret
