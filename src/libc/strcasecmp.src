	.assume	adl=1

	.section	.text
	.global	_strcasecmp
	.type	_strcasecmp, @function

.ifdef PREFER_CE_LIBC

	.set	_strcasecmp, 0x021E3C

.else

_strcasecmp:
	pop	iy
	pop	de
	ex	(sp), hl
	push	de
	ld	b, 1+'z'-'a'
	ld	c, 'a'-'A'
	jr	.L.cmp_loop_entry
.L.cmp_loop:
	cp	a, (hl)
	jr	z, .L.done
.L.loop_nonnull:
	inc	hl
	inc	de
.L.cmp_loop_entry:
	ld	a, (de)
	xor	a, (hl)
	jr	z, .L.cmp_loop
	cp	a, c
	jr	nz, .L.mismatch
	or	a, (hl)
	sub	a, 'a'
	cp	a, b
	jr	c, .L.loop_nonnull

.endif

	.global	_strncasecmp
	.type	_strncasecmp, @function

_strncasecmp:
	pop	iy
	pop	de
	pop	hl
	pop	bc
	push	bc
	push	de
	push	hl

	scf
	sbc	hl, hl
	add	hl, bc
	ex	(sp), hl

	call	c, .L.loop_entry
	jr	c, .L.mismatch
	sbc	hl, hl
	jp	(iy)

.L.checkcase:
	or	a, (hl)
	sub	a, 'a'
	add	a, -(1+'z'-'a')
	ret	c
.L.loop:
	cpi
	ret	po
	ret	z
	inc	de
.L.loop_entry:
	ld	a, (de)
	xor	a, (hl)
	jr	z, .L.loop
	cp	a, 'a'-'A'
	jr	z, .L.checkcase
	scf
	ret

.L.mismatch:
	ld	l, (hl)
	call	_tolower.internal
	ex	de, hl
	ld	l, (hl)
	call	_tolower.internal
	ld	a, l
	sub	a, e
.L.done:
	sbc	hl, hl
	ld	l, a
	jp	(iy)

	.extern	_tolower.internal
