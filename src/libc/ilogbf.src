	assume	adl=1

	section	.text

	public	_ilogbf, _ilogb

; int ilogbf(float)
_ilogb:
_ilogbf:
	ld	hl, 6
	add	hl, sp
	ld	a, (hl)
	dec	hl
	dec	hl
	dec	hl
	ld	hl, (hl)
	add	hl, hl
	adc	a, a
	jr	z, .maybe_subnormal
	inc	a
	jr	z, .inf_nan
	sub	a, 128	; float32_bias + 1
	sbc	hl, hl
	ld	l, a
	ret

.maybe_subnormal:
	add	hl, de
	or	a, a
	sbc	hl, de
	jr	z, .ret_zero
	call	__ictlz
	cpl
	add	a, 130
	sbc	hl, hl
	ld	l, a
	ret

.inf_nan:
	scf
.ret_zero:
	ld	hl, 4	; EDOM
	ld	(_errno), hl
	ld	hl, ___fe_cur_env
	set	4, (hl)	; FE_INVALID
	ld	hl, $800000	; FP_ILOGB0
	ret	nc
	; FP_ILOGBNAN or INT_MAX when carry is set
	dec	hl
	ret

	extern	_errno
	extern	___fe_cur_env
	extern	__ictlz
