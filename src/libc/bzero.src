	.assume	adl=1

	.section	.text

	.global	_bzero
	.type	_bzero, @function

.ifdef PREFER_CE_CRT

; uses the hardware specific $E40000 memory location

; void bzero(void* buf, size_t n)
_bzero:
	ld	hl, 6
	add	hl, sp
	ld	bc, (hl)	; size
	inc	bc
	cpd			; dec hl
	ret	po		; zero size
	dec	hl
	dec	hl
	ld	de, (hl)	; buf
	ld	hl, $E40000	; large region of all zeros on the Ti84CE
	ldir
	ret

.else

; makes no hardware assumptions

; void bzero(void* buf, size_t n)
_bzero:
	ld	hl, 6
	add	hl, sp
	ld	bc, (hl)	; BC = size
	dec	hl
	dec	hl
	dec	hl
	ld	de, (hl)	; DE = dst
	scf
	sbc	hl, hl
	add	hl, bc		; HL = size - 1
	ret	nc		; size == 0
	add	hl, de		; HL = dst + size - 1
	ld	(hl), 0
	cpd			; HL = dst + size - 2, BC = size - 1
	ex	de, hl		; DE = dst + size - 2, HL = dst
	ret	po		; return if size == 1
	add	hl, bc		; HL = dst + size - 1
	lddr
	ret

.endif
