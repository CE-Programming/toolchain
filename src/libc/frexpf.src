	.assume	adl=1

	.section	.text

	.global	_frexpf
	.type	_frexpf, @function
	.global	_frexp
	.type	_frexp, @function

.ifdef PREFER_OS_LIBC

	.set	_frexpf, 0x0220D8
	.set	_frexp, _frexpf

.else

_frexpf:
_frexp:
	ld	iy, 0
	lea	de, iy - 127	; bias	; $FFFF81
	add	iy, sp
	ld	hl, (iy + 3)
	add	hl, hl
	ld	b, (iy + 6)
	ld	a, b
	adc	a, a
	jr	z, .L.maybe_subnormal
	inc	a
	jr	z, .L.inf_nan
	; normal
	ld	b, $7E	; $3F << 1
	rr	b
	sbc	hl, hl
	ld	l, a
;	ld	de, -127	; bias
	add	hl, de
.L.ret_zero:
	ex	de, hl
.L.ret_subnormal:
	res	7, (iy + 5)
.L.ret_self:
	ld	hl, (iy + 9)	; int *expon
	ld	(hl), de
	ld	hl, (iy + 3)	; mantissa
	ld	e, b	; exponent
	ret

.L.inf_nan:
	ld	de, $7FFFFF ; INT_MAX
	jr	.L.ret_self

.L.maybe_subnormal:
	ld	e, d	; ld de, -1
	add	hl, de
	inc	hl	; restore HL
	jr	nc, .L.ret_zero
	; input: HL output: A
	call	__ictlz
	ld	c, a
	call	__ishl
	ld	(iy + 3), hl
	sub	a, 131	; (127 + 3) + 1? idk where this magic number comes from
	cpl
	ld	e, a	; DE was -1 from before
	; copy exponent of 0.5f
	ld	a, b
	xor	a, $3F	
	ld	b, a
	jr	.L.ret_subnormal

	.extern	__ictlz
	.extern	__ishl

.endif
