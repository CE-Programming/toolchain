	assume	adl=1

	section	.text

	public	_frexpf
	public	_frexp

if PREFER_OS_LIBC

_frexpf := 0220D8h
_frexp := _frexpf

else

_frexp:
_frexpf:
	ld	iy, 0
	lea	de, iy - 127	; bias	; $FFFF81
	add	iy, sp
	ld	hl, (iy + 3)
	add	hl, hl
	ld	b, (iy + 6)
	ld	a, b
	adc	a, a
	jr	z, .maybe_subnormal
	inc	a
	jr	z, .inf_nan
	; normal
	ld	b, $7E	; $3F << 1
	rr	b
	sbc	hl, hl
	ld	l, a
;	ld	de, -127	; bias
	add	hl, de
.ret_zero:
	ex	de, hl
.ret_subnormal:
	res	7, (iy + 5)
.ret_self:
	ld	hl, (iy + 9)	; int *expon
	ld	(hl), de
	ld	hl, (iy + 3)	; mantissa
	ld	e, b	; exponent
	ret

.inf_nan:
	ld	de, $7FFFFF ; INT_MAX
	jr	.ret_self

.maybe_subnormal:
	ld	e, d	; ld de, -1
	add	hl, de
	inc	hl	; restore HL
	jr	nc, .ret_zero
	; input: HL output: A
	call	__ictlz
	ld	c, a
	call	__ishl
	ld	(iy + 3), hl
	sub	a, 131	; (127 + 3) + 1? idk where this magic number comes from
	cpl
	ld	e, a	; DE was -1 from before
	; copy exponent of 0.5f
	ld	a, b
	xor	a, $3F	
	ld	b, a
	jr	.ret_subnormal

	extern	__ictlz
	extern	__ishl

end if
